Application MyApp {
    basePackage=org.seke.filmanias

	Module filmanias {

		enum Role {
			MEMBER,
			ADMIN
		}

		Entity User {
			!auditable
			Long id key;
			String username;
			String password;
			String emailAdress;
			boolean accountNonExpired;
			boolean accountNonLocked;
			boolean credentialsNonExpired;
			- @Role role nullable;
			boolean enabled;
			- Set<@Movie> movies <-> user;
			- Set<@Comment> comments <-> user;
			- Set<@Rating> ratings <-> user;
			
			Repository UserDAORepository {
				save;
				findById;
				findAll;
				@User getUser(String username);
				delete;				
			}
		}
		
		Service UserService {
			saveUser(@User user) => UserDAORepository.save;
			retrieveUser(Long id) => UserDAORepository.findById;
			List<@User> retrieveAllUsers() => UserDAORepository.findAll;
			@User retrieveUser(String username) => UserDAORepository.getUser;
			updateUser(@User user) => UserDAORepository.save;
			deleteUser(String username);
		}

		Entity Rating {
			!auditable
			Long id key;
			- @User user key <-> ratings;
			- @Movie movie key <-> ratings;
			int mark;
			Date inputDate;
			
			Repository RatingDAORepository {
				save;
			}
		}

		Service RatingService {
			saveRating(@Rating rating, @User user);
		}
		
		Entity Comment {
			!auditable
			Long id key;
			- @User user key <-> comments;
			- @Movie movie key <-> comments;
			String content;
			Date inputDate;
			
			Repository CommentDAORepository {
				save;
			}
		}

		Service CommentService {
			saveComment(@Comment comment, @User user);
		}
		
		Entity Movie {
			!auditable
			Long id key;
			String name;
			Date inputDate;
			Double rank;
			- @User user nullable <-> movies;
			- Set<@Comment> comments <-> movie;
			- Set<@Rating> ratings <-> movie;
			- Set<@Genre> genres <-> movies;
			
			Repository MovieDAORepository {
				save;
				findAll;
				List<@Movie> retrieveMoviesStartingWith(String movieName);
				findById;
			}
		}
		
		Service MovieService {
			saveMovie(@MovieBean movieBean);
			saveMovie(@Movie movie) => MovieDAORepository.save; 
			List<@Movie> retriveAllMovies() => MovieDAORepository.findAll;
			@Movie retrieveMovie(long id) => MovieDAORepository.findById;
			List<@Movie> retrieveMoviesStartingWith(String movieName) => MovieDAORepository.retrieveMoviesStartingWith;
			addMovieComment(long movieId, @Comment comment,String username);
		}

		ValueObject MovieBean {
			!persistent
    		String name nullable;
			List<GenreBean> allGenres nullable;
			Timestamp inputDate nullable;
			User user nullable;
		}

		Entity Genre {
			!auditable
			String name key;
			- Set<@Movie> movies <-> genres;
			
			Repository GenreDAORepository {
				save;
				findAll;
				@Genre findGenreByName(String name);
			}
		}
		
		Service GenreService {
			save => GenreDAORepository.save;
			getAllGenres => GenreDAORepository.findAll;
			List<@GenreBean> getAllGenreBeans;
			@Genre retrieveGenre(String name) => GenreDAORepository.findGenreByName;
			updateGenre(@Genre genre) => GenreDAORepository.save;			
		}
		
		ValueObject GenreBean {
			!persistent
    		Genre genre;
			boolean assigned;
			String name;
		}
	}
}